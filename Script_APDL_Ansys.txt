!
! EPD_ELIPTICA_MALHA_ANSYS
!
FINISH 
/CLEAR,NOSTART
!
! Parametros geométricos
Rh		= 5		! [m] raio horizontal externo do túnel
Rv		= 5		! [m] raio vertical externo do túnel
R1		= 10*Rv		! [m] raio da região de refinamento da malha próxima do túnel
Lx		= 20*Rv		! [m] dimensão da base do domínio
Ly		= 20*Rv		! [m] dimensão da altura do domínio
!
! Modelos e parâmetros para o material do maciço
!
! Modelo do material do maciço
! matmacico = 1				! elastico do ANSYS
! matmacico = 2				! elastoplastico VM do ANSYS (BISO)
!
matmacico	= 2			! Tipo de modelo escolhido
!
!
!
! Material maciço
E1 		= 403		        ! [MPa] modulo elasticidade
nu1 		= 0.39		        ! [adm] coeficiente de poisson
tipoelem	= 2		        ! 1 - PLANE182, 2 - PLANE183, 3 - PLANE42
!
! Parametros para o modelo 2
!
ctresca         = 2.7
c1	    	= ctresca*SQRT(3)/2     ! =ctresca*SQRT(3)/2 se theta = 0, ou ctresca se theta = 30º	! [MPa]	coesão inicial 
c2	    	= ctresca*SQRT(3)/2     ! =ctresca*SQRT(3)/2 se theta = 0, ou ctresca se theta = 30º    ! [MPa] coesão de pico 
c3	    	= ctresca*SQRT(3)/2     ! =ctresca*SQRT(3)/2 se theta = 0, ou ctresca se theta = 30º	! [MPa] coesão residual 
eps1	    	= 0.024			! [adm] deformação equivalente limite da zona 1 
yield	    	= 2*c1			! [MPa]	tensão de escoamento 
Ep 		= 2*(c2-c1)/(eps1)	! [MPa] módulo plástico tangente 
youngt      	= Ep*E1/(E1+Ep)		! [MPa] módulo de elastoplástico tangente 
!
! Parametros das condições de contorno
pi		= 0		! [MPa] pressão interna
pv		= 8		! [MPa] pressão vertical
ph		= 8		! [MPa] pressão horizontal
!
! Parametros para discretização
nre		= 20		! [un] divisões na interface entre o túnel e o maciço
nr1		= 30		! [un] divisões ao longo do raio da região de refinamento
mr1		= 30		! [%] taxa de crescimento de nr1
nx1		= 10		! [un] divisões na base fora da região de refinamento
mx1		= 1.2		! [%] taxa de crescimento de mx1
ny1		= nx1		! [un] divisões na altura fora da região de refinamento
my1		= mx1		! [%] taxa de crescimento de my1
nrv		= nre/2		! [un] divisões na vertical da parte interna da seção do túnel
nrh		= nre/2		! [un] divisões na horizontal da parte interna da seção do túnel
!
! 2.4 Discretização do tempo nos passos			
!
vkbc		 = 1		! 0 - solicitação linearmente interpolada no passo, 1 - solicitação linearmente interpolada no primeiro subpasso
t1		 = 1		! [incremento de tempo ou partes inteiras da solicitação] do passo
dt 		 = t1/100	! [incremento de tempo ou incremento de partes inteiras da solicitação] a cada subpasso
!
! Configurações de Solução
nr		= 2		! Newton-Raphson
!				! 1 - INIT matriz de rigidez não é atualizada
!				! 2 - FULL matriz de rigidez atualizada a cada iteração
!				! 3 - UNSYM matriz de rigidez atualizada a cada iteração
!
psc		= 0		! 1 - Ativa, 0 - desativa a opção de paralelização SMP
nlg		= 0		! 1 - Ativa, 0 - desativa a não lineariedade geométrica
soleq		= 1		! 1 - calcula o equilibrio do maciço antes de iniciar
!
! Configurações do pós processamento
!	    	     
! Parametros para pós-processamento
arqconv = 'results'	! Nome do arquivo de saida de resultados
!
! Salva os parâmetros em um arquivo de texto
PARSAV,ALL,parametros,txt		! salva os parâmetros da análise
!
! **************************************************************************
/PREP7
!
! Parametros calculados
ratio		= Rv/Rh		
Lx1		= Lx-R1		
Ly1		= Ly-R1
!
! Elemento finito
*IF,tipoelem,EQ,1,THEN
	ET,1,PLANE182
	KEYOPT,1,1,0
	KEYOPT,1,3,2
	KEYOPT,1,6,0
*ELSEIF,tipoelem,EQ,2,THEN
	ET,1,PLANE183
	KEYOPT,1,1,0
	KEYOPT,1,3,2
	KEYOPT,1,6,0
*ELSEIF,tipoelem,EQ,3,THEN
	ET,1,PLANE42
	KEYOPT,1,1,0
	KEYOPT,1,2,1
	KEYOPT,1,3,2
	KEYOPT,1,5,0
	KEYOPT,1,6,0
*ENDIF
!
! Material do maciço
*IF,matmacico,eq,1,then
	! Definindo o material elastico do Ansys
	MPTEMP,1,0  		
	MPDATA,EX,1,,E1	
	MPDATA,PRXY,1,,nu1
	MPDATA,DENS,1,,dens1 	
*ELSEIF,matmacico,eq,2,then
	! Definindo o material elastoplástico VM do Ansys
	MPTEMP,1,0  		
	MPDATA,EX,1,,E1	
	MPDATA,PRXY,1,,nu1
	MPDATA,DENS,1,,dens1
	TB,BISO,1,1,2,  
	TBTEMP,0
	TBDATA,,yield,youngt,,,,
*ENDIF
!
! Keypoints do modelo geométrico
K,1,0,0
K,2,Rh,0
K,3,R1,0
K,4,Lx,0
K,5,0,Rv
K,6,0,R1,
K,7,0,Ly
K,8,Lx,Ly
!
! Criando linhas
L,1,2,nRh
L,2,3,nR1,mR1
L,3,4,ny1,my1
LOCAL,11,1,,,,,,,ratio,
L,5,2,nRe
LOCAL,11,1,,,,,,,1,
L,6,3,nRe
LOCAL,11,0
L,7,8,nRe/2
L,1,5,nRv
L,5,6,nR1,mR1
L,6,7,ny1,my1
L,4,8,nRe/2
!
! Criando áreas
AL,2,4,8,5
AL,3,5,9,6,10
!
! Criando malha
ALLSEL,ALL
LSEL,S,LINE,,6
LSEL,A,LINE,,10
LCCAT,ALL
!
ALLSEL,ALL
MSHAPE,0,2D 
AMESH,ALL
!
! Condição de contorno de simetria
ALLSEL,ALL
LSEL,S,LINE,,8
LSEL,A,LINE,,9
LSEL,A,LINE,,2
LSEL,A,LINE,,3
DL,ALL, ,SYMM
!
! Aplicação das tensões iniciais
ALLSEL,ALL
INISTATE, DEFINE,,,,,-pv,-ph,-2*nu1*ph,0,0,0
!
!
! Condição de contorno de carga
ALLSEL,ALL
LSEL,S,LINE,,6
SFL,ALL,PRESS,pv 
!
ALLSEL,ALL
LSEL,S,LINE,,10
SFL,ALL,PRESS,ph 
!
ALLSEL,ALL
LSEL,S,LINE,,4
SFL,ALL,PRESS,pi
!
! **************************************************************************
! Processamento
/SOLU	
!
! 4.1 Configuração da solução   			    
/SOL			
!
! Configurações iniciais
*IF,nlg,eq,0,then		
	NLGEOM,OFF
*ELSEIF,nlg,eq,1,then		
	NLGEOM,ON
*ENDIF
*IF,nr,eq,1,then		
	NROPT,INIT
*ELSEIF,nr,eq,2,then		
	NROPT,FULL
*ELSEIF,nr,eq,3,then		
	NROPT,UNSYM
*ENDIF	
*IF,psc,eq,0,then		
	PSCONTROL,ALL,OFF
*ELSEIF,nlg,eq,1,then		
	PSCONTROL,ALL,ON
*ENDIF
OUTRES,SVAR,ALL	
OUTRES,ALL,ALL		
ALLSEL,ALL
!
!***********************************************************!
! 4.2 Primeira solução 		   			    !
!***********************************************************!
ANTYPE,0,NEW			! 0 - Análise estática, New - nova
!
! Define o tempo
DELTIM,dt,dt,dt,OFF		! dtime incremento do tempo do passo, incremento mínimo, incremento máximo, chave (ON usa dtime do passo anterior se AUTOTS for ON, OFF - usa dtime no inicio de cada passo)	
TIME,t1
KBC,vkbc
!
! Resolve	
ESEL,S,LIVE
ESEL,ALL
SOLVE   
FINISH 
!
! **************************************************************************
! Pos-processamento
/POST1  
RSYS,1
/DSCALE,ALL,1.0 
!
! Campo de deformações em rr
PLNSOL, U,X, 0,1.0  
!
! Cria caminho
ALLSEL,ALL
NSEL,S,LOC,X,Rh
NSEL,R,LOC,Y,0
*GET,no1,NODE,0,NUM,MAX
!
ALLSEL,ALL
NSEL,S,LOC,X,Lx
NSEL,R,LOC,Y,0
*GET,no2,NODE,0,NUM,MAX
!
ALLSEL,ALL
PATH,caminho,2,30,20,   
PPATH,1,no1
PPATH,2,no2
PDEF, ,U,X,AVG
PDEF, ,S,X,AVG
PDEF, ,S,Y,AVG
PDEF, ,S,Z,AVG
!
! Plota resultados
/PBC,PATH, ,0    
PLPAGM,UX,30,'NODE' 
PLPAGM,SX,30,'NODE' 
PLPAGM,SY,30,'NODE' 
PLPAGM,SZ,30,'NODE' 
